# Section 1.2.1: Bezout's identity

In Section 1.2.1, Knuth introduces induction.

He also states, proves and implements [Bezout's identity](https://en.wikipedia.org/wiki/Euclidean_algorithm#Bézout's_identity) which he calls the "Extended Euclud's algorithm":

Given two positive integers, m, n, we compute the common greates divisor, d, and also integers a, b (not needed to be positive) such that d = am + bn.

As wikipedia notes, this is the prototype of a primary ideal.

Knuth also calls this algorithm E, but I'll call it B:

Input: n, m

1. [ Initialize ] a' = b = 1, a = b' = 0, c = m, d = n
2. [ Divide ] q = c ÷ d; r = c % d (c = qd + r ie m = qn + r)
3. [ Remainder zero? ] r == 0 && return (d=n, a=1, b=q)
4. [ Recycle ] c = d, d = r, t = a', a' = a, a = t-qa, t = b' , b' = b, b = t - qb, goto 2.

This can be done a bit simpler, but we do need to remember a and b over 2 iterations:

1. [ Initialize ] a = b2 = 0, b = a2 = 1
2. [ Divide ] q = m ÷ n; r = m % n (m = qn + r)
4. [ Recycle ] (m, n, a, b, a2, b2) = (n, r, a2 - qa, b2 - qb, a, b)
3. [ Remainder zero? ] r == 0 && return (a, b, m)
4. goto 2

This looks like this in julia:

``` julia
function B(m,n)
    a = b2 = 0; b = a2 = 1
    while (r = m % n) != 0
        q = m ÷ n # integer division
        (m, n, a, b, a2, b2) = (n, r, a2 - q*a, b2 - q*b, a, b)
    end
    return (a, b, n)
end
```


## Speed

The extended Euclid algorithm is already in julia and quite a bit (16x) faster:


``` julia
julia> @btime B(1560,270)
  26.205 ns (0 allocations: 0 bytes)
(4, -23, 30)

julia> @btime gcdx(1560,270)
  1.649 ns (0 allocations: 0 bytes)
(30, 4, -23)
```

The code is actually very simple, so it is not clear why this is so much faster than my version:

``` julia
function gcdx(a::Integer, b::Integer)
    T = promote_type(typeof(a), typeof(b))
    # a0, b0 = a, b
    s0, s1 = oneunit(T), zero(T)
    t0, t1 = s1, s0
    # The loop invariant is: s0*a0 + t0*b0 == a
    x = a % T
    y = b % T
    while y != 0
        q = div(x, y)
        x, y = y, rem(x, y)
        s0, s1 = s1, s0 - q*s1
        t0, t1 = t1, t0 - q*t1
    end
    x < 0 ? (-x, -s0, -t0) : (x, s0, t0)
end
```

And looking at `@code_warntype` it looks like this is actually the code bein gcalled (not as in case of gcd, where some bit-magic was involved.

